CS146 Lecture 18 March 1 2018

### Recall: Interpreter (Haskell)
- Now add statements

```
data AOp = Plus|Minus|Times|Mod|Div

aopTrans Plus = (+)
aopTrans Minus = (-)
aopTrans Times = (*)
aopTrans Mod = mod
aopTrans Div = divisors

data Aexp = Number Integer | Var String | Abin Aop Aexp Aexp

data BOp = Lt|Gt|Le|Ge|Eq

bopTrans Lt = (<)
bopTrans Gt = (>)
bopTrans Le = (<=)
bopTrans Ge = (>=)
bopTrans Eq = (==)

data Bexp = BBin BOp Aexp Aexp
          | Not Bexp
          | And Bexp Bexp
          | Or Bexp Bexp
          | BVal Bool

data Stmt = Skip | Set String Aexp
          | Iif Bexp Stmt Stmt
          | Seq Stmt Stmt | While Bexp Stmt
      (omit print for now)    
```
- SIMP is a program that changes memory - it can be thought of as a function from memory to memory (state to state)
- Need to map variables to values

```
import qualified Data.Map as M
type State = M.Map String Integer

aeval :: Aexp -> State -> integer
aeval (Number n) _ = n
aeval (Var x) s = M.findWithDefault undefined x s
aeval (ABin aop ae1 ae2) s = aopTrans aop (aeval ae1 s) (aeval ae2 s)

beval :: Bexp -> State -> Bool
beval (Bval b) _ = b
beval (Not be) s = not (beval be s)
beval (And be1 be2) s = (beval be1 s) && (beval be2 s)
beval (Or be1 be2) s = (beval be1 s) || (beval be2 s)
beval (BBin op ae1 ae2) = bopTrans op (aeval ae1 s) (aeval ae2 s)

interp :: Stmt -> State -> State
interp skip st = st
interp (set x ae) st = M.insert x (aeval ae st) st
```
- A problem: Haskell is lazy - will substitute first and evaluate later only if it needs to
- For example, if (aeval ae st) has a divide by zero, it will sit in memory instead of crashing
- $! forces the evaluation

```
interp :: Stmt -> State -> State
interp Skip st = st
interp (Set x ae) st = M.insert x $!(aeval ae st) st
interp (Iif be ts fs) st = if (beval be st)
                           then interp ts st
                           else interp fs st
interp (Seq s1 s2) st = let st' = interp s1 st in interp s2 $!st'
interp loop@(while bt body) st = interp (Iif bt (Seq body loop) Skip) st
```
- Need the $!st' since if s2 does not need st' (ie s2 is skip) then s1 will not eval

#### Add printing
- Recall from Module 1
- can model output as a list of characters that *would* be printed (Ï‰ - part of the state)

```
interp:: Stmt -> State -> String -> (State, String)
```
- Add print stmts to the AST:

```
data Stmt = ... | IPrint Aexp | SPrint String
```
- Now update interpreter

```
interp :: Stmt -> State -> String -> (State, String)
interp Skip st om = (st, om)
interp (Set x ae) st om = (M.insert x $!(aeval ae st) st, om)
interp (Iif be ts fs) st om = if (beval be st)
                             then interp ts st om
                             else interp fs st om
interp loop@(while bt body) st om = interp (Iif bt (Seq body loop) Skip) st om
```
- None of these print, so theyre exactly the same with om added
- Sequencing and printing:

```
interp (Seq s1 s2) st om =
    let (st', om') = interp s1 st om
    in interp s2 st' om'
interp (IPrint ae) st om = (st, om++(show (aeval ae st)))
interp (SPrint s) st om = (st, om++s)
```
A program
```
(vars [(x 10) (y 1)]
  (while (> x 0)
         (set y (* 2 y))
         (set x (- x 1))
         (print y)
         (print "\n")))
```
Haskell:
```
p1 = While (BBin Gt (Var "x") (Number 0))
      (Seq (Set "y" (ABin Times (Number 2) (Var "y")))
      (Seq (Set "x" (ABin Minus (Var "x") (Number 1)))
      (Seq (IPrint (Var "y"))
      (SPrint "\n"))))
st = M.fromList[("x", 10), ("y", 1)]
run = let (_, om) = interp p1 st "" in om
```
- returns ```"2\n4\n16\n32\n64\n128\n..."```

##### Another Look
```
interp:: Stmt -> State -> _String -> (State, String)_
```
- Call String -> (\_, String) a "string transformer"
- Factor this out of the type

```
type Mio a = String -> (a, String)

interp:: Stmt -> State -> Mio State
```
Two helper functions
```
inject :: a -> Mio a
inject av = \om -> (av, om)
```
- Turns any value into a string transformer
- Takes any thing and returns a function which takes a string and returns the thing and the string, leaving the string unchanged

```
miprint:: String -> Mio ()
miprint s = \om -> ((), om++s)
```
- Adds s to om

Abstract the behaviour of Seq:

```
interp (Seq s1 s2) st om =
  let (st', om') = interp s1 st om in interp s2 st' om'
```
- interp returns a string transformer
- Need to determine the operations on string transformers
- Algebra of strng transformers
- Expand out the first interp

```
interp (Seq s1 s2) st om =
  (\f -> let (st', om') = f om in interp s2 st' om') (interp s1 st)
```
- Cant pull out the entire second interp since it dependso n st', ie it depends on the other string transformer
- Can pull out interp s2 without st'

```
interp (Seq s1 s2) st om =
  (\f g -> let (st', om') = f om in g st' om') (interp s1 st) (interp s1)
```
New operator: "bind"
```
(>>>=) :: Mio a -> (a -> Mio b) -> Mio b
f >>>= g = \om -> let (av', om') = f om in g av' om'
```
So
```
interp (Seq s1 s2) st = (interp s1 st) >>>= (interp s2)
```
- No longer need om, phrased in terms of string transformers

Special Case: for when g doesn't need the value av (g doesnt depend on f)
```
(>>>):: Mio a -> Mio b -> Mio b
f >>> k = f >>>= \_ -> k
```
Now we can finish the Interpreter
```
interp:: Stmt -> State -> Mio State
interp Skip st = inject st
interp (Set x ae) st = inject (M.insert x $!(aeval ae st) st)
interp (Iif be ts fs) st = if (beval be st) then interp ts st
                            else interp fs st
interp (Seq s1 s2) st =
  interp s1 st >>>= \st' -> interp s2 st'
interp loop@(While bt body) st =
  interp (Iif bt (Seq body loop) Skip) st
interp (IPrint ae) st = miprint (show (aeval ae st)) >>> inject st
interp (SPrint s) st = miprint s >>> inject st
```
- What we did by introducing these operators is exactly how Haskell understands the world
- Haskell has no side effects, yet it can print
- Haskell works in this string transformer world/approach

Native Haskell approach:
- ```Mio a``` = ```IO a```
- ```miprint``` = ```putStr```
- ```>>>=``` = ```>>=```
- ```>>>``` = ```>>```
- ```inject``` = ```return```
