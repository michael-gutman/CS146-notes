CS146 Lecture 16 Feb 27 2018

#### Recall

```
data Op = Plus | Times
opTrans Plus = (+)
opTrans Times = (*)

data Ast = Number Integer
         | Bin Op Ast Ast
         | Fun String Ast
         | App Ast Ast
         | Var String

data Val = Numb Integer
         | Closure String Ast Env

type Env = [(String, Val)]

interp :: Ast -> Env -> Val
interp (Number v) _ = Numb v
interp (Fun p b) e = Closure p b e
interp (Bin op x y) e = Numb (opTrans op v w)
        where (Numb v) = interp x e
              (Numb w) = interp y e
interp (App f x) e = interp fb ((fp, y):fe)
        where
        Closure fp fb fe = interp f e
        y = interp x e
interp (Var x) e = fromMaybe undefined (lookup x e)
```
#### Now lets add set (for mutation) and seq (for sequencing)

```
exp = .... (everything from before)  
    | (set id exp)
    | (seq exp exp)

data Ast = .... | Set String Ast | Seq Ast Ast
data Val = .... | Void
```

- Note: We will implement mutation without actually using mutation
- Haskell doesn't have mutation

Implementing Set
- basic idea: change the name-value binding in the env.
- Needs to be done carefully, Consider:

```
(with ((x  0))
  (+ (seq (set x 5) x)
     (seq (set x 6) x)))
```
- Should produce 11 - but how?
- (set x 5) - ~~Change the~~ Create a new env so that x is now S
  - No mutation in Haskell
- Use that env, when evaluating all the follows
- (set x 6) - similar

So, each expr should return the env that results after it is finished, so that the updated env can be used in what follows\

What about:
```
(with ((x 0))
  (+ (seq (set x 5) x)
     (seq (with ((x 10)) 0) x)))
```
- Should return 10, but could easily return 15 if not careful
- Careful about returning environments

Can this model - threading envs into and out of exprs - work?
- Yes, if you do it right

But how would you accommodate boxes?
- Need a model that permits aliasing

Idea (Recall from previous discussions)
- Env maps vars to locations
- Not threaded, never updated, just added to

Store maps locations -> values
- values are updated, locations are not

Aliasing - Two vars map to the same location

interp - takes a store as an additional param, returns an updated store as a result

```
interp :: Ast -> Env -> Store -> (Val, Store)
type Loc = Integer
type Env = [(String, Loc)]
type Store = [(Loc, Val)]

interp (Number v) _ s = (Numb v, s)
interp (Fun p b) e s = (Closure p b e, s)
interp (Bin op x y) e s = (Numb (opTrans Op v w), s'')
    where
      (Numb v, s') = interp x e s
      (Numb w, s'') = interp y e s'
interp (Seq x y) e s = (v, s'')
    where
      (_, s') = interp x e s
      (v, s'') = interp y e s'
interp (Var x) e s = (fromMaybe undefined (lookup loc s), s)
     where
       loc = fromMaybe undefined (lookup x e)
interp (App f x) e s = interp fb ne ns --interp already produces a store
     where
       (Closure fp fb fe, s') = interp f e s
       (y, s'') = interp x e s'
       nl = newloc s''
       ne = (fp, nl):fe --New env entry
       ns = (nl, y):s'' --New store entry
interp (set x y) e s = (Void, ns)
    where
      (nv, s') = interp y e s
      lx = fromMaybe undefined (lookup x e)
      ns = (lx, nv):s' --lookup will find the first entry

newloc = length --works bc we never remove a loc from the store
                --impractical for long computation
```
- newloc - memory leak city
  - Better: re-use old locations, but its hard ("Garbage Collection, later")

## Module 1 ends here
---
