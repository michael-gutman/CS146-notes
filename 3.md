CS146 Lecture 3
January 11, 2018

# Modelling input
- Infinite sequence consisting of all chars the user will
ever press
  - Model now (Pi, D, w, i)
- Accepting an input char = removing a char from i

Problem:  
- The sequence may depend on the output
- users decide what to press in response to what is displayed on the screen
- A more realistic model might not assume that all the input is available at once

Alternative: A request for input yields a fn consuming one or more chars and producing the next program Pi, with the input characters substituted for the read request

Example:
(read-line) -> \\(line)line  
(user types abc) -> "abc"

Entire program - reduces to a big nesting of input request fns, one fn per "prompt"
- Supply user input for each prompt - yields final result

# Input in Racket
(read-line) - produces a string of all chars pressed until the first newline (string does not contain then newline)

Example:
```
(string->list (read-line))
```
type Test
->
```Scheme
(list #\T #\e \s \t)
```  
Example:
Read in a list of lines
```Scheme
(define (read-input)
  (define nl (read-line))
  (cond [(eof-object? nl) empty]
        [else (cons nl (read-input))]))
```  
more primitive input:  
read-char - extracts one char from the input sequence
peek-char - examines the next character in  the sequence, without removing it

```Scheme
(define (my-read-line)
  (define (mrl-h acc)
    (define ch (read-char))
      (cond [(or (eof-object? ch) (char=? ch #\newline))
              (list->string (reverse acc))]
            [else (mrl-h (cons ch acc))]))
  (mrl-h empty))
```  
less primitive input:   
read - consumes from input (and produces) an S-expression (no matter how many chars or line it occupies)
```Scheme
(define (repl)
  (define exp (read))
  (cond [(eof-object? exp) (void)]
        [else (display (interp (parse exp)))
              (newline)
              (repl)]))
(repl)
```
(have to fill in interp and parse)

Lets write our own read. Process typically happens in 2 steps.
1. Tokenization  
Convert the sequence of raw characters to a sequence of tokens (meaningful "words") eg. (, ), id, number   
  - id - starts with a letter  
  - number - starts with a digit
  - Key observation - peeking at the next char tells us what kind of token we are getting, and what to look for to complete the token

  ```Scheme
  (struct token (type value))
  ```
  Type: 'lp, 'rp 'id 'num  
  "value" of the token (numeric value, name, etc)  
  Helpers:
  ```Scheme
  (define (token-leftpar? x) (symbol=? (token-type x) 'lp))
  (define (token-rightpar? x) (symbol=? (token-type x) 'rp))

  ; read-id: -> (listof char)
  (define (read-id)
    (define nc (peek-char))
    (if (or (char-alphabetic? nc) (char-numeric? nc))
        (cons (read-char) (read-id))
        empty))

  ; read-number: -> (listof char)
  (define (read-number)
    (define nc (peek-char))
    (if (char-numeric? nc)
        (cons (read-char) (read-number))
        empty))
  ```
  Main tokenizer:
  ```Scheme
  ; read-token: -> token
  (define (read-token)
    (define fc (read-char))
    (cond [(char-whitespace? fc) (read-token)]
          [(char=? fc #\( ) (token 'lp fc)]
          [(char=? fc #\) ) (token 'rp fc)]
          [(char-alphabetic? fc)
            (token 'id (list->symbol (cons fc (read-id))))]
          [(char-numeric? fc)
            (token 'id (list->number (cons fc (read-number))))]
          [else (error "lexical error")]))
  ```
  Note: list->symbol, list->number don't exist but easy to build
2. Parsing
  - Are the tokens arranged in to a sequence that has the structure of an S-expression?
  - If so, produce the S-exp  
Helper:

  ```Scheme
  ; read-list: -> (listof sexp)
  (define (read-list) ; assumes left par has already been read
      (define tk (read-token))
      (cond [(token-rightpar? tk) empty]
            [(token-leftpar? tk)
              (cons (read-list) (read-list))]
            [else (cons (token-value tk) (read-list))]))

  ; my-read: -> sexp
  (define (my-read)
      (define tk (read-token))
      (if (token-leftpar? tk)
          (read-list)
          (token-value tk)))
    ```

Exercises
- add support for symbols (symbols can start with numbers, as long as they contain at least one letter)
- handle arbitrary kinds of brackets
  - (a [b c {d e} f])
  - they have to match
- generalizes HTML/XML/JSON parser
